import dependencies.Versions
import dependencies.Libs

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${Libs.Compose.version}"
    }
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Compose.foundation
    implementation Libs.Compose.ui
    implementation Libs.Compose.ui_util

    testImplementation Libs.Test.junit
    androidTestImplementation Libs.AndroidX.Test.junit
    androidTestImplementation Libs.AndroidX.Test.espresso
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.github.fornewid'
                artifactId = 'material-motion-compose'
                version = '0.1.0'

                pom {
                    name = 'material-motion-compose'
                    description = 'Material Motion for Jetpack Compose'
                    url = 'https://github.com/fornewid/material-motion-compose'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'fornewid'
                            name = 'SOUP'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com/fornewid/material-motion-compose.git'
                        developerConnection = 'scm:git@github.com/fornewid/material-motion-compose.git'
                        url = 'https://github.com/fornewid/material-motion-compose'
                    }
                }
            }
        }
    }
}
